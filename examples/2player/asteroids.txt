BasicGame no_players=2
    SpriteSet
        base > Immovable color=WHITE img=base
        moving >
            avatar  > ShootAvatar img=spaceship
                avatar1 > stype=sam1
                avatar2 > stype=sam2
            missile > Missile timeToLive=20
                sam1  > color=BLUE singleton=True img=missile
                sam2  > color=RED singleton=True img=missile
        asteroid > RandomNPC img=boulder 
            big > speed=0.05 cons=1
            medium > speed=0.10 cons=2 shrinkfactor=0.8
            small > speed=0.15 cons=3 shrinkfactor=0.5
        portal  > SpawnPoint stype=big cooldown=16 total=5 invisible=True hidden=True
    
    LevelMapping
        p > portal
        b > base
        A > avatar1
        B > avatar2

    TerminationSet
        SpriteCounter stype=avatar1 limit=0 win=False,True
        SpriteCounter stype=avatar2 limit=0 win=True,False
        SpriteCounter stype=asteroid limit=0 count_score=True
        TimeoutScoreCount limit=2000
        
    InteractionSet
        moving EOS > wrapAround
        asteroid EOS > stepBack
        
        avatar avatar > killBoth
        avatar1 sam2 > killSprite scoreChange=-2,10
        avatar2 sam1 > killSprite scoreChange=10,-2
        
        missile big > spawn stype=medium
        missile big > spawn stype=medium
        missile big > killBoth
		
        missile medium > spawn stype=small
        missile medium > spawn stype=small
        missile medium > killBoth
		
        sam1 small > killBoth scoreChange=3,0
        sam2 small > killBoth scoreChange=0,3
        avatar1 asteroid > killBoth scoreChange=-1,0
        avatar2 asteroid > killBoth scoreChange=0,-1
		
        base asteroid > killSprite
        base sam1 > killBoth scoreChange=1,0
        base sam2 > killBoth scoreChange=0,1
        avatar base > stepBack