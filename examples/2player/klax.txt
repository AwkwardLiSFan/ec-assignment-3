BasicGame no_players=2

    SpriteSet
        movable >
            blockSpawn  > BomberRandomMissile stypeMissile=red,green,yellow hidden=True invisible=True singleton=True cooldown=4 speed=0.8 prob=0.1
            avatar  > HorizontalAvatar img=avatar
                none > 
                    avatarA > 
                    avatarB > 
                claimed > 
                    avatarAC > img=alien
                    avatarBC > img=avatarBlue
        missile > Missile orientation=DOWN speed=0.1
            unclaimed >
                red  > color=LIGHTRED
                green  > color=LIGHTGREEN
                yellow  > color=LIGHTYELLOW
            avA >
                redA > color=RED shrinkfactor=0.5
                greenA  > color=GREEN shrinkfactor=0.5
                yellowA  > color=YELLOW shrinkfactor=0.5
            avB >
                redB > color=PINK shrinkfactor=0.5
                greenB  > color=DARKGRAY shrinkfactor=0.5
                yellowB  > color=BROWN shrinkfactor=0.5

    InteractionSet
        blockSpawn wall  > reverseDirection
        movable wall  > stepBack
        missile wall  > killSprite
		
        avatarA red > updateSpawnType stype=redA spawnPoint=blockSpawn
        avatarA red > transformToAll stype=red stypeTo=redA
		
        avatarA green > updateSpawnType stype=greenA spawnPoint=blockSpawn
        avatarA green > transformToAll stype=green stypeTo=greenA
		
        avatarA yellow > updateSpawnType stype=yellowA spawnPoint=blockSpawn
        avatarA yellow > transformToAll stype=yellow stypeTo=yellowA
        
        avatarA avA > transformTo stype=avatarAC
        avA avatarAC > killSprite scoreChange=2,0
        avB avatarAC > killSprite scoreChange=1,0
        avatarAC unclaimed > removeScore killSecond=True
				
        avatarB red > updateSpawnType stype=redB spawnPoint=blockSpawn
        red avatarB > transformToAll stype=red stypeTo=redB
		
        avatarB green > updateSpawnType stype=greenB spawnPoint=blockSpawn
        green avatarB > transformToAll stype=green stypeTo=greenB
		
        avatarB yellow > updateSpawnType stype=yellowB spawnPoint=blockSpawn
        yellow avatarB > transformToAll stype=yellow stypeTo=yellowB
        
        avatarB avB > transformTo stype=avatarBC
        avB avatarBC > killSprite scoreChange=0,2
        avA avatarBC > killSprite scoreChange=0,1
        avatarBC unclaimed > removeScore killSecond=True
 		
    LevelMapping
        s > blockSpawn
        A > avatarA
        B > avatarB

    TerminationSet
        TimeoutScoreCount limit=1500