#Bolo Adventures III: http://www.mobygames.com/game/dos/bolo-adventures-iii

BasicGame
    SpriteSet
        hole   > Immovable color=DARKBLUE img=hole
        goal > Immovable img=door
        avatar > OrientedAvatar rotateInPlace=false
        box    > Passive img=box
        boulder  > Passive img=boulder
        boulderm > Missile img=boulder
        lasercannon > SpawnPoint 
            lcup > img=spaceship stype=lup
            lcdown > img=spaceship stype=ldown
            lcright > img=spaceship stype=lright
            lcleft > img=spaceship stype=lleft
        laser > Missile color=YELLOW  shrinkfactor=0.5 speed=0.5
            lup > orientation=UP
            ldown > orientation=DOWN
            lright > orientation=RIGHT
            lleft > orientation=LEFT
        pushingbox > Resource limit=2 value=0

    LevelMapping
        A > avatar
        B > box
        C > boulder
        L > lcleft
        R > lcright
        U > lcup
        D > lcdown
        0 > hole
        G > goal

    InteractionSet

        boulderm wall box boulder lasercannon > transformTo stype=boulder
        boulderm hole > killBoth scoreChange=1

        avatar wall lasercannon > stepBack
        #avatar box > shieldFrom ftype=killSprite stype=laser
        avatar box > changeResource resource=pushingbox value=1
        box avatar  > bounceForward
        box wall box hole lasercannon   > undoAll
        
        avatar wall > stepBack
        boulder avatar  > transformTo stype=boulderm
        boulderm avatar > attractGaze
        avatar boulder boulderm > stepBack

        boulder box wall boulder lasercannon > undoAll
        laser wall boulder boulderm box hole > killSprite

        #avatar laser > killSprite
        avatar laser > changeResource resource=pushingbox value=-1
        avatar laser > killIfHasLess resource=pushingbox limit=0
        avatar hole > stepBack
        
        goal avatar > killSprite scoreChange=5

    TerminationSet
        SpriteCounter stype=avatar    limit=0 win=False
        SpriteCounter stype=goal    limit=0 win=True